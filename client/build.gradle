apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'

group = "org.urbanbyte.cueserver"
version = "1.1-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:1.0.2'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.1.1'
    }
}

dependencies {
   compile 'com.google.guava:guava:18.0',
           'org.apache.httpcomponents:httpclient:4.3.5',
           'org.slf4j:slf4j-api:1.7.5'
    testCompile 'org.mockito:mockito-core:1.10.7',
                'junit:junit:4.11'
}

jacoco {
    toolVersion = "0.7.1.201405082137"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugs {
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    reportsDir = file("$project.buildDir/findbugsReports")
    effort = "max"
    reportLevel = "low"
    visitors = ["FindSqlInjection", "SwitchFallthrough"]
    omitVisitors = ["FindNonShortCircuit"]
}

compileJava {
    options.compilerArgs = ['-Xlint:all']
}

compileTestJava {
    options.compilerArgs = ['-Xlint:all']
}

jar {
    manifest {
        attributes 'Implementation-Title': 'CueServer HTTP Client',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}


modifyPom {
    project {
        name 'CueServer HTTP client'
        description 'Client library for the CueServer HTTP interface.'
        url 'https://urbanbyte.org'
        inceptionYear '2014'
        artifactId = "cueserver-client"

        scm {
            url 'https://github.com/creising/CueServer-Client'
            connection 'scm:https://github.com/creising/CueServer-Client.git'
            developerConnection 'scm:https://github.com/creising/CueServer-Client.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'creising'
                name 'Christopher Reising'
                email 'creising@gmail.com'
            }
        }
    }

}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}
